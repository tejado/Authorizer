/*
 * Copyright (Â©) 2016 Jeff Harris <jefftharris@gmail.com>
 * All rights reserved. Use of the code is allowed under the
 * Artistic License 2.0 terms, as specified in the LICENSE file
 * distributed with this code, or available from
 * http://www.opensource.org/licenses/artistic-license-2.0.php
 */
import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'
    defaultConfig {
        applicationId "com.jefftharris.passwdsafe"
        minSdkVersion 10
        resConfigs "de", "fr"
        targetSdkVersion 23
        versionCode 60403
        versionName "6.4.3"

        ndk {
            moduleName "PasswdSafe"
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles += 'proguard-rules.pro'
        }
    }
    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
    }
}


task buildNative(type: Exec, description: 'Compile JNI source') {
    def ndkDir = project.plugins.findPlugin('android').sdkHandler.ndkFolder
    def ndkBuild = Os.isFamily(Os.FAMILY_WINDOWS) ? "ndk-build.cmd" : "ndk-build"
    commandLine "$ndkDir/$ndkBuild",
            '-C', file('src/main/jni').absolutePath,
            '-j', Runtime.runtime.availableProcessors(),
            'all'

}


task cleanNative(type: Exec, description: 'Clean JNI object files') {
    def ndkDir = project.plugins.findPlugin('android').sdkHandler.ndkFolder
    def ndkBuild = Os.isFamily(Os.FAMILY_WINDOWS) ? "ndk-build.cmd" : "ndk-build"
    commandLine "$ndkDir/$ndkBuild",
            '-C', file('src/main/jni').absolutePath,
            'clean'
}


clean.dependsOn 'cleanNative'


tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn "buildNative"
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    debugCompile project(path: ':lib', configuration: 'debug')
    releaseCompile project(path: ':lib', configuration: 'release')
    compile 'com.android.support:preference-v7:23.4.0'
    compile 'com.android.support:preference-v14:23.4.0'

    androidTestCompile 'com.android.support:support-annotations:23.4.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
}
